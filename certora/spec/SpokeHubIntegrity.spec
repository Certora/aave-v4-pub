
import "./SpokeBase.spec";
import "./symbolicRepresentation/SymbolicPositionStatus.spec";
import "./symbolicRepresentation/ERC20s_CVL.spec";

// Note: 'spoke' alias is declared in SpokeBase.spec
using Hub as hub;


// Methods block for Spoke contract
methods {
    // Note: LiquidationLogic._calculateLiquidationAmounts is summarized in SpokeBase.spec

    function _.setInterestRateData(uint256 assetId, bytes data) external => NONDET; 

    function _._checkCanCall(address caller, bytes calldata data) internal => NONDET; 

    function AssetLogic.getDrawnIndex(IHub.Asset storage   asset) internal returns (uint256) => cachedIndex;
}

// assume a given single drawnIndex
ghost uint256 cachedIndex;


ghost mapping(uint256 /*reserveId*/ => mathint /*source*/) sumUserSuppliedSharesPerReserveId {
    init_state axiom forall uint256 reserveId. sumUserSuppliedSharesPerReserveId[reserveId] == 0;
}

// Hook on sstore and sload to synchronize the ghost with storage changes
hook Sstore _userPositions[KEY address user][KEY uint256 reserveId].suppliedShares uint120 newValue (uint120 oldValue) {
    sumUserSuppliedSharesPerReserveId[reserveId] = sumUserSuppliedSharesPerReserveId[reserveId] + newValue - oldValue;
}

hook Sload uint120 value _userPositions[KEY address user][KEY uint256 reserveId].suppliedShares {
    require sumUserSuppliedSharesPerReserveId[reserveId] >= value;
}

ghost mapping(uint256 /*reserveId*/ => mathint /*source*/) sumUserDrawnSharesPerReserveId {
    init_state axiom forall uint256 reserveId. sumUserDrawnSharesPerReserveId[reserveId] == 0;
}

// Hook on sstore and sload to synchronize the ghost with storage changes
hook Sstore _userPositions[KEY address user][KEY uint256 reserveId].drawnShares uint120 newValue (uint120 oldValue) {
    sumUserDrawnSharesPerReserveId[reserveId] = sumUserDrawnSharesPerReserveId[reserveId] + newValue - oldValue;
}

hook Sload uint120 value _userPositions[KEY address user][KEY uint256 reserveId].drawnShares {
    require sumUserDrawnSharesPerReserveId[reserveId] >= value;
}

ghost mapping(uint256 /*reserveId*/ => mathint /*source*/) sumUserPremiumSharesPerReserveId {
    init_state axiom forall uint256 reserveId. sumUserPremiumSharesPerReserveId[reserveId] == 0;
}

// Hook on sstore and sload to synchronize the ghost with storage changes
hook Sstore _userPositions[KEY address user][KEY uint256 reserveId].premiumShares uint120 newValue (uint120 oldValue) {
    sumUserPremiumSharesPerReserveId[reserveId] = sumUserPremiumSharesPerReserveId[reserveId] + newValue - oldValue;
}

hook Sload uint120 value _userPositions[KEY address user][KEY uint256 reserveId].premiumShares {
    require sumUserPremiumSharesPerReserveId[reserveId] >= value;
}

ghost mapping(uint256 /*reserveId*/ => mathint /*source*/) sumUserPremiumOffsetPerReserveId {
    init_state axiom forall uint256 reserveId. sumUserPremiumOffsetPerReserveId[reserveId] == 0;
}

// Hook on sstore and sload to synchronize the ghost with storage changes
hook Sstore _userPositions[KEY address user][KEY uint256 reserveId].premiumOffsetRay int200 newValue (int200 oldValue) {
    sumUserPremiumOffsetPerReserveId[reserveId] = sumUserPremiumOffsetPerReserveId[reserveId] + to_mathint(newValue) - to_mathint(oldValue);
}


}





invariant userDrawnShareConsistency(uint256 reserveId, uint256 assetId_) 
    sumUserDrawnSharesPerReserveId[reserveId] == hub._spokes[spoke._reserves[reserveId].assetId][spoke].drawnShares &&
    ( reserveId >= spoke._reserveCount => 
        sumUserDrawnSharesPerReserveId[reserveId] == 0
    ) &&
    ( !spoke._reserveExists[hub][assetId_] => 
        hub._spokes[assetId_][spoke].drawnShares == 0
    ) 
    filtered {f -> !outOfScopeFunctions(f)}
    {
        preserved  with (env e) {
            require e.msg.sender != spoke;
            safeAssumptions();
        }
        preserved addReserve(address hub_, uint256 assetId_arg, address priceSource, ISpoke.ReserveConfig config, ISpoke.DynamicReserveConfig  dynamicConfig) with (env e) {
            require hub_ == hub && assetId_arg == assetId_;
            safeAssumptions();
        }
        preserved repay(uint256 otherReserveId, uint256 amount, address onBehalfOf) with (env e) {
            safeAssumptions();
            //proved in spoke.spec : uniqueAssetIdPerReserveId
            require (reserveId < spoke._reserveCount && otherReserveId < spoke._reserveCount && reserveId != otherReserveId ) => (spoke._reserves[reserveId].assetId != spoke._reserves[otherReserveId].assetId );
        }
        preserved borrow(uint256 otherReserveId, uint256 amount, address onBehalfOf) with (env e) {
            safeAssumptions();
            //proved in spoke.spec : uniqueAssetIdPerReserveId
            require (reserveId < spoke._reserveCount && otherReserveId < spoke._reserveCount && reserveId != otherReserveId ) => (spoke._reserves[reserveId].assetId != spoke._reserves[otherReserveId].assetId );
        }

    }

invariant userPremiumShareConsistency(uint256 reserveId, uint256 assetId_) 
    sumUserPremiumSharesPerReserveId[reserveId] == hub._spokes[spoke._reserves[reserveId].assetId][spoke].premiumShares &&
    ( reserveId >= spoke._reserveCount => 
        sumUserPremiumSharesPerReserveId[reserveId] == 0
    ) &&
    ( !spoke._reserveExists[hub][assetId_] => 
        hub._spokes[assetId_][spoke].premiumShares == 0
    ) 
    filtered {f -> !outOfScopeFunctions(f)}
    {
        preserved  with (env e) {
            require e.msg.sender != spoke;
            safeAssumptions();
        }
       preserved addReserve(address hub_, uint256 assetId_arg, address priceSource, ISpoke.ReserveConfig config, ISpoke.DynamicReserveConfig  dynamicConfig) with (env e) {
            require hub_ == hub && assetId_arg == assetId_;
            safeAssumptions();
        }
    }

invariant userPremiumOffsetConsistency(uint256 reserveId, uint256 assetId_) 
    sumUserPremiumOffsetPerReserveId[reserveId] == hub._spokes[spoke._reserves[reserveId].assetId][spoke].premiumOffsetRay &&
    ( reserveId >= spoke._reserveCount => 
        sumUserPremiumOffsetPerReserveId[reserveId] == 0
    ) &&
    ( !spoke._reserveExists[hub][assetId_] => 
        hub._spokes[assetId_][spoke].premiumOffsetRay == 0
    ) 
    filtered {f -> !outOfScopeFunctions(f)}
    {
        preserved  with (env e) {
            require e.msg.sender != spoke;
            safeAssumptions();
        }
       preserved addReserve(address hub_, uint256 assetId_arg, address priceSource, ISpoke.ReserveConfig config, ISpoke.DynamicReserveConfig  dynamicConfig) with (env e) {
            require hub_ == hub && assetId_arg == assetId_;
            safeAssumptions();
        }
    }
/*
invariant userRealizedPremiumConsistency(uint256 reserveId, uint256 assetId_) 
    sumUserRealizedPremiumPerReserveId[reserveId] == hub._spokes[spoke._reserves[reserveId].assetId][spoke].realizedPremiumRay &&
    ( reserveId >= spoke._reserveCount => 
        sumUserRealizedPremiumPerReserveId[reserveId] == 0
    ) &&
    ( !spoke._reserveExists[hub][assetId_] => 
        hub._spokes[assetId_][spoke].realizedPremiumRay == 0
    ) 
    filtered {f -> !outOfScopeFunctions(f)}
    {
        preserved  with (env e) {
            require e.msg.sender != spoke;
            safeAssumptions();
        }
        preserved addReserve(address hub_, uint256 assetId_arg, address priceSource, ISpoke.ReserveConfig config, ISpoke.DynamicReserveConfig  dynamicConfig) with (env e) {
            require hub_ == hub && assetId_arg == assetId_;
            safeAssumptions();
        }
    }
*/
// this does not pass because of the fee receiver check and the transferFeeShares which might lock shares
invariant userSuppliedShareConsistency(uint256 reserveId, uint256 assetId_) 
    sumUserSuppliedSharesPerReserveId[reserveId] <= hub._spokes[spoke._reserves[reserveId].assetId][spoke].addedShares
    && 
    ( reserveId >= spoke._reserveCount => 
        sumUserSuppliedSharesPerReserveId[reserveId] == 0
    )
    filtered {f -> !outOfScopeFunctions(f)}
    {
        preserved  with (env e) {
            require e.msg.sender != spoke;
            safeAssumptions();
            require hub._assets[spoke._reserves[reserveId].assetId].feeReceiver != spoke;
            require hub._assets[assetId_].feeReceiver != spoke;
        }
        preserved addReserve(address hub_, uint256 assetId_arg, address priceSource, ISpoke.ReserveConfig config, ISpoke.DynamicReserveConfig  dynamicConfig) with (env e) {
            require hub_ == hub && assetId_arg == assetId_;
            safeAssumptions();
            require hub._assets[spoke._reserves[reserveId].assetId].feeReceiver != spoke;
            require hub._assets[assetId_].feeReceiver != spoke;
        }
    }



/*
//todo - check this violation, seems that debt can increse a bit no health check
//https://prover.certora.com/output/40726/7b75b670ab0b4dc099147c08e2b33527/ 
rule checkRepay_withHubLinked(uint256 reserveId, uint256 amount, address user) {
    env e;
    safeAssumptions();
    require userGhost == user;
    
    uint120 beforeUserPosition_drawnShares = spoke._userPositions[user][reserveId].drawnShares;
    uint200 beforeUserPosition_realizedPremiumRay = spoke._userPositions[user][reserveId].realizedPremiumRay;

    spoke.repay(e, reserveId, amount, user);

    uint120 afterUserPosition_drawnShares = spoke._userPositions[user][reserveId].drawnShares;
    uint200 afterUserPosition_realizedPremiumRay = spoke._userPositions[user][reserveId].realizedPremiumRay;
    
    satisfy beforeUserPosition_drawnShares == afterUserPosition_drawnShares;
    satisfy beforeUserPosition_realizedPremiumRay < afterUserPosition_realizedPremiumRay;
    satisfy beforeUserPosition_drawnShares == afterUserPosition_drawnShares && beforeUserPosition_realizedPremiumRay < afterUserPosition_realizedPremiumRay;

}

*/

rule checkRepay_zeroDebt(uint256 reserveId, uint256 amount, address user) {
    env e;
    require spoke._userPositions[user][reserveId].drawnShares == 0 => (  spoke._userPositions[user][reserveId].premiumShares == 0 && spoke._userPositions[user][reserveId].premiumOffsetRay == 0);

    spoke.repay(e, reserveId, amount, user);

    assert spoke._userPositions[user][reserveId].drawnShares == 0 => (  spoke._userPositions[user][reserveId].premiumShares == 0 && spoke._userPositions[user][reserveId].premiumOffsetRay == 0);
}

function safeAssumptions() {
    // rules proved in spoke.spec and assuming one hub
    require forall uint256 reserveId. forall uint256 otherReserveId. 
    (reserveId != otherReserveId ) => spoke._reserves[reserveId].assetId != spoke._reserves[otherReserveId].assetId ;

    // a reservid that exists has underlying and hub
    require forall uint256 reserveId. (reserveId < spoke._reserveCount  => 
    // has underlying and hub
    (spoke._reserves[reserveId].underlying != 0 && spoke._reserves[reserveId].hub == hub && spoke._reserveExists[spoke._reserves[reserveId].hub][spoke._reserves[reserveId].assetId] ));

    // a reservid that does not exist has no underlying, hub, assetId
    require forall uint256 reserveId. reserveId >= spoke._reserveCount => (
    // has no underlying, hub, assetId
    spoke._reserves[reserveId].underlying == 0 && spoke._reserves[reserveId].assetId == 0 && spoke._reserves[reserveId].hub == 0  && spoke._reserves[reserveId].dynamicConfigKey == 0); 

    // based on hubValidState.spec : validAssetId 
    require forall uint256 assetId. assetId >= hub._assetCount => (
        hub._assets[assetId].addedShares == 0 &&
        hub._assets[assetId].drawnShares == 0 &&
        hub._assets[assetId].premiumShares == 0 &&
        hub._assets[assetId].premiumOffsetRay == 0 &&
        hub._assets[assetId].drawnIndex == 0 &&
        hub._assets[assetId].drawnRate == 0 &&
        hub._assets[assetId].lastUpdateTimestamp == 0 &&
        hub._spokes[assetId][spoke].addedShares == 0 &&
        hub._spokes[assetId][spoke].drawnShares == 0 &&
        hub._spokes[assetId][spoke].premiumShares == 0  &&
        hub._spokes[assetId][spoke].premiumOffsetRay == 0 &&
        !hub._spokes[assetId][spoke].active
        );

}